buildscript {
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.10.1' apply false
    id "com.jfrog.bintray" version "1.8.4"  apply false
    id "com.jfrog.artifactory" version "4.9.7" apply false
    id 'jacoco'
    id 'idea'
}

ext {
    v_commons_codec = '1.12'
    v_guava = '22.0'
    v_jacksondatabind = '2.9.8'
    v_junit = '4.12'
    v_lombok = '1.18.8'
    v_slf4j = '1.7.26'
    v_spring_framework = '5.1.8.RELEASE'
    v_validation_api = '1.1.0.Final'
    ENV_MAVEN_PUBLIC_URL = System.getenv('MAVEN_PUBLIC_URL')
    ENV_MAVEN_RELEASE_URL = System.getenv('MAVEN_RELEASE_URL')
    ENV_MAVEN_SNAPSHOT_URL = System.getenv('MAVEN_SNAPSHOT_URL')
    ENV_MAVEN_REPO_USERNAME = System.getenv('MAVEN_REPO_USERNAME')
    ENV_MAVEN_REPO_PASSWORD = System.getenv('MAVEN_REPO_PASSWORD')
}

allprojects {

    group = 'one.kii.summer'

    repositories {
        maven {
            if ("${ENV_MAVEN_PUBLIC_URL}" != 'null') {
                url "${ENV_MAVEN_PUBLIC_URL}"
            } else {
                url "${maven_public_url}"
            }

        }
    }

}

subprojects {
    apply plugin: 'maven-publish'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'pl.allegro.tech.build.axion-release'
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'com.jfrog.bintray'

    scmVersion {
        tag {
            prefix = project.name
        }
    }
    project.version = scmVersion.version

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                if (project.version.endsWith('-SNAPSHOT')) {
                    credentials {
                        if ("${ENV_MAVEN_REPO_USERNAME}" != 'null') {
                            username "${ENV_MAVEN_REPO_USERNAME}"
                        } else {
                            username "$maven_username"
                        }
                        if ("${ENV_MAVEN_REPO_PASSWORD}" != 'null') {
                            password "${ENV_MAVEN_REPO_PASSWORD}"
                        } else {
                            password "$maven_password"
                        }
                    }
                    if ("${ENV_MAVEN_SNAPSHOT_URL}" != 'null') {
                        url "${ENV_MAVEN_SNAPSHOT_URL}"
                    } else {
                        url "${maven_snapshot_url}"
                    }
                } else {
                    credentials {
                        if ("${ENV_MAVEN_REPO_USERNAME}" != 'null') {
                            username "${ENV_MAVEN_REPO_USERNAME}"
                        } else {
                            username "$maven_username"
                        }
                        if ("${ENV_MAVEN_REPO_PASSWORD}" != 'null') {
                            password "${ENV_MAVEN_REPO_PASSWORD}"
                        } else {
                            password "$maven_password"
                        }
                    }
                    if ("${ENV_MAVEN_RELEASE_URL}" != 'null') {
                        url "${ENV_MAVEN_RELEASE_URL}"
                    } else {
                        url "${maven_release_url}"
                    }
                }
            }
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        provided
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
            test.compileClasspath += configurations.provided
            test.runtimeClasspath += configurations.provided
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${v_lombok}"
        annotationProcessor "org.projectlombok:lombok:${v_lombok}"
        testCompileOnly "org.projectlombok:lombok:${v_lombok}"
        compileOnly("javax.validation:validation-api:${v_validation_api}")
        testCompile("javax.validation:validation-api:${v_validation_api}")


        compile "org.springframework:spring-beans:${v_spring_framework}"
        compile("org.springframework:spring-tx:${v_spring_framework}")
        compile("org.springframework:spring-context:${v_spring_framework}")
        compile("org.slf4j:slf4j-api:${v_slf4j}")

        testCompile "junit:junit:${v_junit}"
        testCompile("org.springframework:spring-test:${v_spring_framework}")
        testCompile("org.slf4j:slf4j-simple:${v_slf4j}")

    }

    // https://reflectoring.io/publish-snapshots-with-gradle/
    artifactory {
        contextUrl = 'https://oss.jfrog.org'
        publish {
            repository {
                repoKey = 'oss-snapshot-local'
                username = "${ENV_MAVEN_REPO_USERNAME}" != 'null' ? "${ENV_MAVEN_REPO_USERNAME}" : "${maven_username}"
                password = "${ENV_MAVEN_REPO_PASSWORD}" != 'null' ? "${ENV_MAVEN_REPO_PASSWORD}" : "${maven_password}"
            }
            defaults {
                publications('mavenJava')
                publishArtifacts = true
                publishPom = true
            }
        }
        resolve {
            repoKey = 'jcenter'
        }
        clientConfig.info.setBuildNumber(System.getProperty('build.number'))
    }

    bintray {
        user = "${ENV_MAVEN_REPO_USERNAME}" != 'null' ? "${ENV_MAVEN_REPO_USERNAME}" : "${maven_username}"
        key = "${ENV_MAVEN_REPO_PASSWORD}" != 'null' ? "${ENV_MAVEN_REPO_PASSWORD}" : "${maven_password}"
        publications = ['mavenJava']
        pkg {
            repo = 'maven'
            name = 'summer'
            userOrg = 'kiiworld'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/sinewang/summer.git'
        }
    }

}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        // @See https://stackoverflow.com/questions/55918239/could-not-find-method-destination-for-arguments-on-report-xml-of-type-org-grad
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

